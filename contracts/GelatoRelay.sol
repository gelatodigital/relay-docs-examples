// SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import {IGelatoRelay} from "./interfaces/IGelatoRelay.sol";
import {IGelato1Balance} from "./interfaces/IGelato1Balance.sol";
import {GelatoRelayBase1Balance} from "./abstract/GelatoRelayBase1Balance.sol";
import {GelatoCallUtils} from "./lib/GelatoCallUtils.sol";
import {GelatoTokenUtils} from "./lib/GelatoTokenUtils.sol";
import {
    _encodeRelayerContext
} from "@gelatonetwork/relayer-context/contracts/functions/RelayerUtils.sol";
import {_eip2771Context} from "./functions/ContextUtils.sol";
import {
    SponsorAuthCallWith1Balance,
    UserAuthCallWith1Balance,
    UserSponsorAuthCallWith1Balance
} from "./types/CallTypes.sol";
import {IGelato} from "./interfaces/IGelato.sol";
import {PaymentType} from "./types/PaymentTypes.sol";

/// @title  Gelato Relay contract
/// @notice This contract deals with synchronous payments and Gelato 1Balance payments
/// @dev    This contract must NEVER hold funds!
/// @dev    Maliciously crafted transaction payloads could wipe out any funds left here
// solhint-disable-next-line max-states-count
contract GelatoRelay is IGelatoRelay, IGelato1Balance, GelatoRelayBase1Balance {
    using GelatoCallUtils for address;
    using GelatoTokenUtils for address;

    //solhint-disable-next-line const-name-snakecase
    string public constant name = "GelatoRelay";
    //solhint-disable-next-line const-name-snakecase
    string public constant version = "1";

    // solhint-disable-next-line no-empty-blocks
    constructor(address _gelato) GelatoRelayBase1Balance(_gelato) {}

    // TO DO: after RelayerContext is properly implemented on Gelato
    // remove _feeToken and _fee and expect _data to be Context encoded.
    /// @notice Relay call with Synchronous Payment
    /// @notice The target contract pays Gelato during the call forward
    /// @dev    This is the most straightforward use case, and `transfer` handles token payments.
    /// @param _target Target smart contract
    /// @param _data Payload for call on _target
    /// @param _feeToken Payment can be done in native and ERC-20 tokens
    /// @param _taskId Unique task identifier generated by gelato
    function callWithSyncFee(
        address _target,
        bytes calldata _data,
        address _feeToken,
        uint256 _fee,
        bytes32 _taskId
    ) external onlyGelato {
        uint256 preBalance = _feeToken.getBalance(msg.sender);

        // TO DO: remove hacky way and replace with
        // implementation that _encodes RelayerContext on Gelato
        _target.revertingContractCall(
            _encodeRelayerContext(_data, msg.sender, _feeToken, _fee),
            "GelatoRelay.callWithSyncFee:"
        );

        uint256 postBalance = _feeToken.getBalance(msg.sender);

        uint256 fee = postBalance - preBalance;

        emit LogCallWithSyncFee(_target, _feeToken, fee, _taskId);
    }

    /// @notice Relay call + One Balance payment - with sponsor authentication
    /// @notice Sponsor signature allows for payment via sponsor's 1Balance balance
    /// @dev    Payment is handled with off-chain accounting using Gelato's 1Balance system
    /// @param _call Relay call data packed into SponsorAuthCall struct
    /// @param _sponsorSignature EIP-712 compliant signature from _call.sponsor
    /// @notice Oracle value for exchange rate between native tokens and fee token
    /// @param  _nativeToFeeTokenXRateNumerator Exchange rate numerator
    /// @param  _nativeToFeeTokenXRateDenominator Exchange rate denominator
    /// @param _taskId Unique task identifier generated by gelato
    // solhint-disable-next-line function-max-lines
    function sponsorAuthCallWith1Balance(
        SponsorAuthCallWith1Balance calldata _call,
        bytes calldata _sponsorSignature,
        uint256 _nativeToFeeTokenXRateNumerator,
        uint256 _nativeToFeeTokenXRateDenominator,
        bytes32 _taskId
    ) external onlyGelato {
        // CHECKS
        require(
            _call.paymentType == PaymentType.OneBalance,
            "GelatoRelay.sponsorAuthCallWith1Balance: paymentType"
        );

        _requireChainId(
            _call.chainId,
            "GelatoRelay.sponsorAuthCallWith1Balance:"
        );

        // Do not enforce ordering on nonces,
        // but still enforce replay protection
        // via uniqueness of message
        bytes32 digest = _requireSponsorAuthCallSignature1Balance(
            _getDomainSeparator(),
            _call,
            _sponsorSignature,
            _call.sponsor
        );
        require(
            !wasCallSponsoredAlready[digest],
            "GelatoRelay.sponsorAuthCallWith1Balance: replay"
        );

        // EFFECTS
        wasCallSponsoredAlready[digest] = true;

        // INTERACTIONS
        _call.target.revertingContractCall(
            _call.data,
            "GelatoRelay.sponsorAuthCallWith1Balance:"
        );

        emit LogUseGelato1Balance(
            _call.sponsor,
            _call.target,
            _call.feeToken,
            _call.oneBalanceChainId,
            _nativeToFeeTokenXRateNumerator,
            _nativeToFeeTokenXRateDenominator,
            _taskId
        );
    }

    /// @notice Relay call + One Balance payment - with ONLY user authentication.
    /// @notice The user here is paying for themselves, no external sponsor
    /// @notice User signature allows for payment via user's 1Balance balance
    /// @dev    Payment is handled with off-chain accounting using Gelato's 1Balance system
    /// @dev    The userNonce abstraction does not support multiple calls (call concurrency)
    /// @dev    Apps that need concurrent user calls will need to implement multi-calling
    /// @dev    on their end via encoding into _call.data.
    /// @param _call Relay call data packed into UserAuthCall struct
    /// @param _userSignature EIP-712 compliant signature from _call.user
    /// @notice Oracle value for exchange rate between native tokens and fee token
    /// @param  _nativeToFeeTokenXRateNumerator Exchange rate numerator
    /// @param  _nativeToFeeTokenXRateDenominator Exchange rate denominator
    /// @param _taskId Unique task identifier generated by gelato
    // solhint-disable-next-line function-max-lines
    function userAuthCallWith1Balance(
        UserAuthCallWith1Balance calldata _call,
        bytes calldata _userSignature,
        uint256 _nativeToFeeTokenXRateNumerator,
        uint256 _nativeToFeeTokenXRateDenominator,
        bytes32 _taskId
    ) external onlyGelato {
        // CHECKS
        require(
            _call.paymentType == PaymentType.OneBalance,
            "GelatoRelay.userAuthCallWith1Balance: paymentType"
        );

        _requireChainId(_call.chainId, "GelatoRelay.userAuthCallWith1Balance:");

        uint256 storedUserNonce = userNonce[_call.user];

        // For the user, we enforce nonce ordering
        _requireUserBasics(
            _call.userNonce,
            storedUserNonce,
            _call.userDeadline,
            "GelatoRelay.userAuthCallWith1Balance:"
        );

        _requireUserAuthCallSignature1Balance(
            _getDomainSeparator(),
            _call,
            _userSignature,
            _call.user
        );

        // EFFECTS
        userNonce[_call.user] = storedUserNonce + 1;

        // INTERACTIONS
        _call.target.revertingContractCall(
            _eip2771Context(_call.data, _call.user),
            "GelatoRelay.userAuthCallWith1Balance:"
        );

        emit LogUseGelato1Balance(
            _call.user,
            _call.target,
            _call.feeToken,
            _call.oneBalanceChainId,
            _nativeToFeeTokenXRateNumerator,
            _nativeToFeeTokenXRateDenominator,
            _taskId
        );
    }

    /// @notice Relay call + One Balance payment - with BOTH sponsor and user authentication
    /// @notice Both sponsor and user signature allows for payment via sponsor's 1Balance balance
    /// @dev    Payment is handled with off-chain accounting using Gelato's 1Balance system
    /// @dev    The userNonce abstraction does not support multiple calls (call concurrency)
    /// @dev    Apps that need concurrent user calls will need to implement multi-calling
    /// @dev    on their end via encoding into _call.data.
    /// @param _call Relay call data packed into UserSponsorAuthCall struct
    /// @param _userSignature EIP-712 compliant signature from _call.user
    /// @param _sponsorSignature EIP-712 compliant signature from _call.sponsor
    /// @param  _nativeToFeeTokenXRateNumerator Exchange rate numerator
    /// @param  _nativeToFeeTokenXRateDenominator Exchange rate denominator
    /// @param _taskId Unique task identifier generated by gelato
    // solhint-disable-next-line function-max-lines
    function userSponsorAuthCallWith1Balance(
        UserSponsorAuthCallWith1Balance calldata _call,
        bytes calldata _userSignature,
        bytes calldata _sponsorSignature,
        uint256 _nativeToFeeTokenXRateNumerator,
        uint256 _nativeToFeeTokenXRateDenominator,
        bytes32 _taskId
    ) external onlyGelato {
        // CHECKS
        require(
            _call.paymentType == PaymentType.OneBalance,
            "GelatoRelay.userSponsorAuthCallWith1Balance: paymentType"
        );

        _requireChainId(
            _call.chainId,
            "GelatoRelay.userSponsorAuthCallWith1Balance:"
        );

        uint256 storedUserNonce = userNonce[_call.user];

        // For the user, we enforce nonce ordering
        _requireUserBasics(
            _call.userNonce,
            storedUserNonce,
            _call.userDeadline,
            "GelatoRelay.userSponsorAuthCallWith1Balance:"
        );

        bytes32 domainSeparator = _getDomainSeparator();

        // Verify user's signature
        _requireUserSponsorAuthCallSignature1Balance(
            domainSeparator,
            _call,
            _userSignature,
            _call.user
        );

        // Verify sponsor's signature
        // Do not enforce ordering on nonces but still enforce replay protection
        // via uniqueness of call with nonce
        bytes32 digest = _requireUserSponsorAuthCallSignature1Balance(
            domainSeparator,
            _call,
            _sponsorSignature,
            _call.sponsor
        );

        // Sponsor replay protection
        require(
            !wasCallSponsoredAlready[digest],
            "GelatoRelay.userSponsorAuthCallWith1Balance: replay"
        );

        // EFFECTS
        userNonce[_call.user] = storedUserNonce + 1;
        wasCallSponsoredAlready[digest] = true;

        // INTERACTIONS
        _call.target.revertingContractCall(
            _eip2771Context(_call.data, _call.user),
            "GelatoRelay.userSponsorAuthCallWith1Balance:"
        );

        emit LogUseGelato1Balance(
            _call.sponsor,
            _call.target,
            _call.feeToken,
            _call.oneBalanceChainId,
            _nativeToFeeTokenXRateNumerator,
            _nativeToFeeTokenXRateDenominator,
            _taskId
        );
    }

    //solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32) {
        return _getDomainSeparator();
    }

    function _getDomainSeparator() internal view returns (bytes32) {
        return
            keccak256(
                abi.encode(
                    keccak256(
                        bytes(
                            //solhint-disable-next-line max-line-length
                            "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
                        )
                    ),
                    keccak256(bytes(name)),
                    keccak256(bytes(version)),
                    block.chainid,
                    address(this)
                )
            );
    }
}
